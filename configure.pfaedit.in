dnl Process this file with autoconf to produce a configure script.

AC_INIT(pfaedit/pfaedit.h)
AC_PROG_CC
#AC_PROG_RANLIB
#AC_DISABLE_SHARED
AC_DISABLE_STATIC
AC_CANONICAL_HOST
AC_PROG_LIBTOOL

gww_nodynamic="no"
case "$host" in
*-apple-darwin*) AC_DEFINE(NODYNAMIC)
gww_nodynamic="yes"
AC_DEFINE(_CursorsMustBe16x16)
AC_DEFINE(_Keyboard,1)
 ;;
powerpc-*-*linux*)
AC_DEFINE(_Keyboard,3)
 ;;
*sparc*)
AC_DEFINE(_Keyboard,2)
 ;;
*-pc-cygwin*)
AC_DEFINE(_BrokenBitmapImages)
AC_DEFINE(_ModKeysAutoRepeat)
 ;;
esac

AC_SEARCH_LIBS(dlopen, dl) dnl some systems have this in libc (Darwin uses dyld)
AC_SEARCH_LIBS(inet_aton, resolv) dnl some systems have this in libc

dnl Is there a better way to add a directory to the include path?

CFLAGS="$CFLAGS -I/usr/include/freetype2/"

dnl Some old versions of lib(un)gif don't work for us, they've got the
dnl Function argument in the wrong place. So two checks for gif.

AC_CHECK_LIB(ungif, DGifOpenFileName, [
echo -n checking for ExtensionBlock.Function in gif_lib.h...
AC_TRY_COMPILE([#include <gif_lib.h>], [ ExtensionBlock foo; foo.Function=3; ], echo " " yes , [echo " " no
    ac_cv_lib_ungif_DGifOpenFileName=no
    AC_DEFINE(_NO_LIBUNGIF)])
] , AC_DEFINE(_NO_LIBUNGIF))

AC_CHECK_LIB(z, deflateEnd, : ,AC_DEFINE(_NO_LIBPNG), -lm) 
AC_CHECK_LIB(png, png_create_read_struct, : ,AC_DEFINE(_NO_LIBPNG), -lz -lm) 
AC_CHECK_LIB(jpeg, jpeg_CreateDecompress, :  ,AC_DEFINE(_NO_LIBJPEG)) 
AC_CHECK_LIB(tiff, TIFFOpen, : , AC_DEFINE(_NO_LIBTIFF), -lm )
AC_CHECK_LIB(freetype, FT_New_Face, : ,AC_DEFINE(_NO_FREETYPE)) 

dnl older libc's don't have snprintf

AC_CHECK_FUNC(snprintf, : , AC_DEFINE(_NO_SNPRINTF))

AC_C_CONST
AC_PATH_XTRA

dnl If we can't/don't want to use dynamic libraries then we must link with
dnl the static libraries we've found

AC_SUBST(STATIC_LIBS)
STATIC_LIBS=
if test "$gww_nodynamic" = "yes"; then
 if test "$ac_cv_lib_ungif_DGifOpenFileName" = "yes"; then
  STATIC_LIBS="$STATIC_LIBS -lungif"
 fi
 if test "$ac_cv_lib_png_png_create_read_struct" = "yes"; then
  STATIC_LIBS="$STATIC_LIBS -lpng -lz"
 fi
 if test "$ac_cv_lib_png_tiff_TIFFOpen" = "yes"; then
  STATIC_LIBS="$STATIC_LIBS -ltiff"
 fi
 if test "$ac_cv_lib_png_jpeg_jpeg_CreateDecompress" = "yes"; then
  STATIC_LIBS="$STATIC_LIBS -ljpeg"
 fi
fi

test -d libs || mkdir libs

AC_OUTPUT(Makefile Unicode/Makefile gdraw/Makefile pfaedit/Makefile)

echo Configuration defines: $DEFS

dnl EOF
